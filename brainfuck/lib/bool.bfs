################################################################################

#if(x) code1 else code2
ifelse(x;code1;code2;temp0;temp1)
{
	!
	? temp0 0() +      #temp0 = 1
	? temp1 0()        #temp1 = 0
	? x     [          #if x
	?           code1  #    code1
	? temp0     -      #    temp0--
	? x         [      #    while x
	? temp1         +  #        temp1++
	? x         -]     #        x--
	? x     ]
	? temp1 [          #while temp1
	? x     +          #    x++
	? temp1 -]         #    temp1--
	? temp0 [          #if temp0
	?           code2  #    code2
	? temp0 -]         #    temp0--
	? ~
}

#y = bool(x)
bool(x;y;temp0)
{
	!
	? y     0()    #y = 0
	? x     [      #while x != 0
	? x     -      #x--
	? temp0 +      #temp0++
	? y     0() +  #y = 1
	? x     ]
	? temp0 [      #while temp0 != 0
    ? temp0 -      #temp0--
	? x     +      #x++
	? temp0 ]
	? ~
}

#z = x or y
or(x;y;z;temp0;temp1)
{
	bool(x;z;temp0)     #z = bool(x)
	bool(y;temp0;temp1) #temp0 = bool(y)

	!
	#temp0 = bool(z) + bool(y)
	? z     [           #while z
	? z     -           #z--
	? temp0 +           #temp0++
	? z     ]

	#z = bool(bool(z) + bool(y))
	? temp0 [           #while temp0
	? temp0 -           #temp0--
	? z     0() +       #z = 1
	? temp0 ]
	? ~
}

