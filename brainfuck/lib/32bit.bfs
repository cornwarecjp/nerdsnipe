################################################################################

read32(x)
{
	!
	? x ,>,>,>,
	?~
}

write32(x)
{
	!
	? x .>.>.>.
	?~
}

const32(x;a;b;c;d;e;f;g;h;temp0)
{
	!
	? x     0() a
	?       *10(x    ;temp0)
	? x     b

	? x >   0() c
	?       *10(x >  ;temp0)
	? x >   d

	? x >>  0() e
	?       *10(x >> ;temp0)
	? x >>  f

	? x >>> 0() g
	?       *10(x >>>;temp0)
	? x >>> h

	? ~
}


zero32(x)
{
	! x
	0() > 0() > 0() > 0()
	? ~
}

#y = x; x = 0
move32(x;y)
{
	move8(x    ;y    )
	move8(x >  ;y >  )
	move8(x >> ;y >> )
	move8(x >>>;y >>>)
}

#y = x
#temp0 can be 8-bit
copy32(x;y;temp0)
{
	copy8 (x    ;y    ;temp0)
	copy8F(x >  ;y >  ;temp0)
	copy8F(x >> ;y >> ;temp0)
	copy8F(x >>>;y >>>;temp0)
}

#y += x
#tempx can be 8-bit
ad32(y;x;temp0;temp1;temp2;temp3;temp4)
{
	combineCarry()
	{
		!
		? temp1 [     #if temp1
		? temp0 0() + #    temp0 = 1
		? temp1 -     #    temp1 = 0
		? temp1 ]     #
		?~
	}

	adc8(y >>>;x >>>;temp0;temp2;temp3;temp4) #temp0 = cout

	adc8(y >> ;temp0;temp1;temp2;temp3;temp4) #temp0 = cin, temp1 = cout
	adc8(y >> ;x >> ;temp0;temp2;temp3;temp4) #temp0 = cout
	combineCarry()

	adc8(y >  ;temp0;temp1;temp2;temp3;temp4) #temp0 = cin, temp1 = cout
	adc8(y >  ;x >  ;temp0;temp2;temp3;temp4) #temp0 = cout
	combineCarry()

	adc8(y    ;temp0;temp1;temp2;temp3;temp4) #temp0 = cin, temp1 = cout
	adc8(y    ;x    ;temp0;temp2;temp3;temp4) #temp0 = cout
}

#z = x ^ y
#z may be x or y
#tempx can be 8-bit
xor32(z;x;y;temp0;temp1;temp2;temp3;temp4;temp5;temp6;temp7)
{
	xor8(z    ;x    ;y    ;temp0;temp1;temp2;temp3;temp4;temp5;temp6;temp7)
	xor8(z >  ;x >  ;y >  ;temp0;temp1;temp2;temp3;temp4;temp5;temp6;temp7)
	xor8(z >> ;x >> ;y >> ;temp0;temp1;temp2;temp3;temp4;temp5;temp6;temp7)
	xor8(z >>>;x >>>;y >>>;temp0;temp1;temp2;temp3;temp4;temp5;temp6;temp7)
}

#z = x & y
#z may be x or y
#tempx can be 8-bit
and32(z;x;y;temp0;temp1;temp2;temp3;temp4;temp5;temp6;temp7)
{
	and8(z    ;x    ;y    ;temp0;temp1;temp2;temp3;temp4;temp5;temp6;temp7)
	and8(z >  ;x >  ;y >  ;temp0;temp1;temp2;temp3;temp4;temp5;temp6;temp7)
	and8(z >> ;x >> ;y >> ;temp0;temp1;temp2;temp3;temp4;temp5;temp6;temp7)
	and8(z >>>;x >>>;y >>>;temp0;temp1;temp2;temp3;temp4;temp5;temp6;temp7)
}

#y = ~x
#temp0 can be 8-bit
not32(y;x;temp0)
{
	not8(y    ;x    ;temp0)
	not8(y >  ;x >  ;temp0)
	not8(y >> ;x >> ;temp0)
	not8(y >>>;x >>>;temp0)
}

#x >>= 1; c=carry
#c is 8-bit
#temp0;temp1;temp2;temp3 can be 8-bit
shr32(x;c;temp0;temp1;temp2;temp3)
{
	addCarry(c;dest)
	{
		*10(c;temp0) # c is now 000x0000
		!
		? c    [        #|dest += 8*c
		? dest ++++++++ #|c = 0
		? c    -        #|
		? c    ]        #|
		?~
	}

	shr8(x >>>;c    ;temp0;temp1;temp2)
	shr8(x >> ;temp3;temp0;temp1;temp2)
	addCarry(temp3;x >>>)
	shr8(x >  ;temp3;temp0;temp1;temp2)
	addCarry(temp3;x >> )
	shr8(x    ;temp3;temp0;temp1;temp2)
	addCarry(temp3;x >  )
}

#x rr= 1; c=carry
#c is 8-bit
#temp0;temp1;temp2;temp3;temp4 can be 8-bit
rr32(x;temp0;temp1;temp2;temp3;temp4)
{
	addCarry(c;dest)
	{
		*10(c;temp0) # c is now 000x0000
		!
		? c    [        #|dest += 8*c
		? dest ++++++++ #|c = 0
		? c    -        #|
		? c    ]        #|
		?~
	}

	shr8(x >>>;temp4;temp0;temp1;temp2)
	shr8(x >> ;temp3;temp0;temp1;temp2)
	addCarry(temp3;x >>>)
	shr8(x >  ;temp3;temp0;temp1;temp2)
	addCarry(temp3;x >> )
	shr8(x    ;temp3;temp0;temp1;temp2)
	addCarry(temp3;x >  )
	addCarry(temp4;x    )
}


#z = x >> y
#y is 8-bit
#tempx can be 8-bit
rightshift32(z;x;y;temp0;temp1;temp2;temp3;temp4;temp5)
{
	copy32(x;z;temp0)
	copy8(y;temp5;temp0)
	while(temp5;
		shr32(z;temp0;temp1;temp2;temp3;temp4)
		decr8(temp5)
		)
}


#z = x rr y
#y is 8-bit
#tempx can be 8-bit
rightrotate32(z;x;y;temp0;temp1;temp2;temp3;temp4;temp5)
{
	copy32(x;z;temp0)
	copy8(y;temp5;temp0)
	while(temp5;
		rr32(z;temp0;temp1;temp2;temp3;temp4)
		decr8(temp5)
		)
}

