################################################################################

read8(x)
{
	!
	? x ,
	?~
}

write8(x)
{
	!
	? x .
	?~
}

decr8(x)
{
	!
	? x -
	?~
}

#x = 0
0(x){! x [-] ?~}

zero8(x){0(x)}

#y = x; x = 0
move8(x;y)
{
	0(y)  #y = 0
	move8F(x;y)
}
#same, but requires zero-value target
move8F(x;y)
{
	!
	? x [       #while x
	? x     -   #    x--
	? y     +   #    y++
	? x ]
	? ~
}


#y = x
copy8(x;y;temp0)
{
	0(temp0)
	copy8F(x;y;temp0)
}
#same, but requires zero-value temp0
copy8F(x;y;temp0)
{
	!
	? y     0()   #y = 0
	? x     [     #while x
	? x     -     #    x--
	? y     +     #    y++
	? temp0 +     #    temp0++
	? x     ]
	? temp0 [     #while temp0
	? temp0 -     #    temp0--
	? x     +     #    x++
	? temp0 ]
	? ~
}




+0(){}
+1(){+}
+2(){++}
+3(){+++}
+4(){++++}
+5(){+++++}
+6(){++++++}
+7(){+++++++}
+8(){++++++++}
+9(){+++++++++}
+a(){++++++++++}
+b(){+++++++++++}
+c(){++++++++++++}
+d(){+++++++++++++}
+e(){++++++++++++++}
+f(){+++++++++++++++}
*10(x;temp0)
{
	!
	?       move8(x;temp0)   #temp0 = x; x = 0
	? temp0 [                #while temp0 != 0
	? temp0 -                #    temp0--
	? x     ++++++++++++++++ #    x += 0x10
	? temp0 ]
	? ~
}

const8(x;a;b;temp0)
{
	!
	? x 0() a
	?   *10(x;temp0)
	? x b
	?~
}

#y -= x
st8(y;x;temp0)
{
	!
	? temp0 0()   #temp0 = 0
	? x     [     #while x
	? x     -     #    x--
	? y     -     #    y--
	? temp0 +     #    temp0++
	? x     ]
	? temp0 [     #while temp0
	? temp0 -     #    temp0--
	? x     +     #    x++
	? temp0 ]
	? ~
}

#z = y - x
subtract8(z;y;x;temp0)
{
	copy8(y;z;temp0)
	st8(z;x;temp0)
}

#y += x
ad8(y;x;temp0)
{
	!
	? temp0 0()   #temp0 = 0
	? x     [     #while x
	? x     -     #    x--
	? y     +     #    y++
	? temp0 +     #    temp0++
	? x     ]
	? ~
	move8F(temp0;x)
}

#y += x; c = carry
adc8(y;x;c;temp0;temp1;temp2)
{
	0(c)     #c = 0
	0(temp0) #temp0 = 0
	0(temp1) #temp1 = 0
	0(temp2) #temp2 = 0

	!
	? x     [                     #while x
	? x     -                     #    x--
	? temp0 +                     #    temp0++
	? y     +                     #    y++
	?       copy8F(y;temp1;temp2) #    temp1 = y    |temp2 = (y == 0)
	? temp2 +                     #    temp2 = 1    |
	? temp1 [                     #    if temp1     |
	? temp2 -                     #        temp2 = 0|
	? temp1 0()                   #                 |
	? temp1 ]                     #                 |
	? temp2 [                     #    if temp2
	? c     +                     #        c = 1
	? temp2 -                     #
	? temp2 ]                     #
	? x     ]                     #
	?~

	move8F(temp0;x)
}

#z = y + x
add8(z;y;x;temp0)
{
	copy8(y;z;temp0)
	ad8(z;x;temp0)
}

#z = x OP y
#z may be x or y
#OP takes xbit,ybit as input and stores output in zbit
#OP must set xbit,ybit to 0
bitwise8(z;x;y;OP;xbit;ybit;zbit;temp0;temp1;temp2;temp3;temp4)
{
	copy8(x;temp0;temp2)#temp0 = x
	copy8(y;temp1;temp2)#temp1 = y
	zero8(z)            #z = 0

	!
	? zero8(temp2)     #temp2 = 0
	? temp2 +8()       #temp2 = 8  |repeat 8 times:
	? while(temp2;     #while temp2|
	?     decr8(temp2) #    temp2--|

	?     shr8(z;temp3;temp4;xbit;zbit)     #z /= 2
	?     shr8(temp0;xbit;temp3;temp4;zbit) #temp0 /= 2; xbit = carry
	?     shr8(temp1;ybit;temp3;temp4;zbit) #temp1 /= 2; ybit = carry

	?     OP

	?     *10(zbit;temp3) #    zbit *= 0x10|if zbit:
	?     zbit [          #    while zbit  |    z += 0x80
	?     zbit -          #        zbit--  |
	?     z    +8()       #        z += 8  |
	?     zbit ]          #                |
	?     )

	?~
}

#z = x ^ y
#z may be x or y
xor8(z;x;y;temp0;temp1;temp2;temp3;temp4;temp5;temp6;temp7)
{
	xbit(){temp0}
	ybit(){temp1}
	zbit(){temp2}

	OP()
	{
		#One bit xor
		!
		? zero8(zbit()) #    zbit = 0
		? ybit() [      #    if ybit
		? ybit() -      #        ybit = 0
		? zbit() +      #        zbit = 1
		? xbit() [      #        if xbit
		? xbit() -      #            xbit = 0
		? zbit() -      #            zbit = 0
		? xbit() ]      #
		? ybit() ]      #
		? xbit() [      #    if xbit
		? xbit() -      #        xbit = 0
		? zbit() +      #        zbit = 1
		? xbit() ]      #
		?~
	}

	bitwise8(z;x;y;OP();xbit();ybit();zbit();temp3;temp4;temp5;temp6;temp7)
}

#z = x & y
#z may be x or y
and8(z;x;y;temp0;temp1;temp2;temp3;temp4;temp5;temp6;temp7)
{
	xbit(){temp0}
	ybit(){temp1}
	zbit(){temp2}

	OP()
	{
		#One bit and
		!
		? zbit() 0()    #    zbit = 0
		? ybit() [      #    if ybit
		? ybit() -      #        ybit = 0
		? xbit() [      #        if xbit
		? xbit() -      #            xbit = 0
		? zbit() +      #            zbit = 1
		? xbit() ]      #
		? ybit() ]      #
		?~
	}

	bitwise8(z;x;y;OP();xbit();ybit();zbit();temp3;temp4;temp5;temp6;temp7)
}

#y = not x
not8(y;x;temp0)
{
	copy8(x;temp0;y) #temp0 = x; y = 0

	!
	? temp0 +
	? temp0 [
	? temp0 +
	? y     +
	? temp0 ]
	?~
}

#x >>= 1; c=carry
shr8(x;c;temp0;temp1;temp2)
{
	copy8(x;temp0;temp1)              #temp0 = x; temp1 = 0
	zero8(c)                          #c = 0
	zero8(temp2)                      #temp2 = 0

	!
	? temp0 [                         #while temp0:

	? c     zero8() +                 #    c = 1        |c = temp0==1
	?       copy8F(temp0;temp1;temp2) #    temp1 = temp0|
	? temp1 -                         #    temp1--      |
	? temp1 [                         #    if temp1:    |
	?       zero8(c)                  #        c = 0    |
	?       zero8(temp1)              #                 |
	? temp1 ]                         #                 |

	? x     -                         #    x--
	? temp0 --                        #    temp0 -= 2

	?       copy8F(c;temp1;temp2)     #    temp1 = c    |if c:
	? temp1 [                         #    if temp1:    |    temp0 = 0
	?       zero8(temp0)              #        temp0 = 0|
	? temp1 -                         #                 |
	? temp1 ]                         #                 |

	? temp0 ]
	?~
}

